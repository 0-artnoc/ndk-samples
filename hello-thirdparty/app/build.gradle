apply plugin: 'com.android.model.application'

// Find ndk path, used to config native_app_glue srcDir
def ndkDir = System.getenv("ANDROID_NDK_HOME")
def propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    Properties properties = new Properties()
    properties.load(propertiesFile.newDataInputStream())
    ndkDir = properties.getProperty('ndk.dir')
}

// Google play game static lib path
def gpg_sdk_path = file(project(':thirdparty:gpg-sdk').projectDir).absolutePath +
                   "/gpg-cpp-sdk/android"

model {

    repositories {
        libs(PrebuiltLibraries) {
            gpg {
                headers.srcDir "${gpg_sdk_path}/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("${gpg_sdk_path}/lib/gnustl/${targetPlatform.getName()}/libgpg.a")
                }
            }
        }
    }

    android {
        compileSdkVersion = 23
        buildToolsVersion = "23.0.2"

        defaultConfig {
            //
            //  NOTE: change the application ID to match the play console linked application.
            //
            applicationId = "com.example.android.hello_thirdparty"
            minSdkVersion.apiLevel    = 9
            targetSdkVersion.apiLevel = 23
        }

        ndk {
            moduleName = "native-activity"
            platformVersion = defaultConfig.minSdkVersion.apiLevel
            stl    = "c++_static"  // gnustl_static does not work
            cppFlags.addAll(["-std=c++11",
                             // issues/165 -- Need update gpg::functions to enable "-Wall", "-Werror",
                             "-I" + ndkDir + "/sources/android/native_app_glue"])
            ldLibs.addAll(["log", "android", "EGL", "GLESv1_CM",
                           "atomic", "z"]) // gpg depends on atomic & z libs
            // Filtering ABIs on the ones Google Play Games library supports
            abiFilters.addAll(["armeabi", "armeabi-v7a", "x86", "arm64-v8a"])
        }
        sources {
            main {
                jni {
                    dependencies {
                        library "gpg" linkage "static"
                    }
                    source {
                        srcDir "${ndkDir}/sources/android/native_app_glue"
                    }
                }
            }
        }
        buildTypes {
            release {
                minifyEnabled = false
            }
        }
    }
}

dependencies {
    compile 'com.google.android.gms:play-services-games:8.3.0'
    compile 'com.google.android.gms:play-services-base:8.3.0'
    compile 'com.google.android.gms:play-services-appstate:8.3.0'
    compile 'com.google.android.gms:play-services-nearby:8.3.0'
}
