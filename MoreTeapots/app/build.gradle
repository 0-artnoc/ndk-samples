apply plugin: 'com.android.model.application'

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = "23.0.2"

        defaultConfig.with {
            applicationId    = "com.sample.moreteapots"
            minSdkVersion.apiLevel    = 17
            targetSdkVersion.apiLevel = 22
        }
    }

    android.ndk {
        moduleName = "MoreTeapotsNativeActivity"
        cppFlags.addAll(["-I${file("../../Common/cpufeatures")}".toString(),
                         "-I${file("../../Common/native_app_glue")}".toString(),
                         "-I${file("../../Common/ndk_helper")}".toString()])
        ldLibs.addAll(["android", "EGL", "GLESv2","log", "atomic"])
        stl     = "c++_static"
    }
    android.sources {
        main {
            jni {
                source {
                    srcDirs 'src/main/jni'
                    srcDirs '../../Common'
                }
            }
        }
    }
    // Turn on hard float support in armeabi-v7a
    android.abis {
        create("armeabi-v7a") {
            cppFlags.addAll(["-mhard-float", "-D_NDK_MATH_NO_SOFTFP=1", "-mfloat-abi=hard"])
            ldLibs.add("m_hard")
            ldFlags.add("-Wl,--no-warn-mismatch")
        }
    }
    android.productFlavors {
        create ("arm7") {
            ndk.abiFilters.add("armeabi-v7a")
        }
        create ("arm8") {
            ndk.abiFilters.add("arm64-v8a")
        }
        create ("x86-32") {
            ndk.abiFilters.add("x86")
        }
        // for detailed abiFilter descriptions, refer to "Supported ABIs" @
        // https://developer.android.com/ndk/guides/abis.html#sa
        // build one including all productFlavors
        create("all")
    }
}
